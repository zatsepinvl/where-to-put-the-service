plugins {
    id "org.springframework.boot" version "2.5.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java"
    //https://youtrack.jetbrains.com/issue/IDEA-151925?_ga=2.75699861.105379761.1613457260-781677120.1613140272#comment=27-2355076
    id "idea"
}

group = "com.effective"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "16"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.intTest.java.srcDirs
        testSourceDirs += project.sourceSets.intTest.resources.srcDirs
    }
}

repositories {
    mavenCentral()
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
    intTestCompileOnly.extendsFrom compileOnly
    intTestAnnotationProcessor.extendsFrom annotationProcessor
}

def junitVersion = "5.7.0"
def postgresqlVersion = "42.1.4"
def testcontainersVersion = "1.15.2"

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    annotationProcessor "org.projectlombok:lombok"
    compileOnly "org.projectlombok:lombok"
    compileOnly "org.postgresql:postgresql:$postgresqlVersion"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    intTestImplementation "org.springframework.boot:spring-boot-starter-test"
    intTestImplementation "org.testcontainers:junit-jupiter"
    intTestImplementation "org.testcontainers:testcontainers"
    intTestImplementation "org.testcontainers:postgresql"
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:$testcontainersVersion"
    }
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = "Runs integration tests."
    group = "verification"

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

integrationTest {
    useJUnitPlatform()
}

check.dependsOn integrationTest
